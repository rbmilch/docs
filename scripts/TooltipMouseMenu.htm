<!DOCTYPE HTML>
<html>
<head>
<title>ToolTip-Mausmenü (benötigt XP/2k/NT) -- von Rajat</title>
<meta http-equiv="content-type" content="text/html; charset=UTF-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
<script src="../static/ga.js" type="text/javascript"></script>
</head>
<body>

<h1>ToolTip-Mausmenü (benötigt XP/2k/NT) -- von Rajat</h1>

<p>Dieses Skript zeigt ein aufklappbares Menü beim kurzen Drücken der mittleren Maustaste an. Ein Menüpunkt kann mit einem Linksklick ausgewählt werden. Das Menü wird geschlossen, sobald außerhalb des Menüs mit der linken Maustaste geklickt wird. Als aktuelle Verbesserung kann der Inhalt des Menüs geändert werden, abhängig davon, welcher Fenstertyp aktiv ist (Notepad und Word wurden hier als Beispiele verwendet).
</p>
<p><a href="TooltipMouseMenu.ahk">Dieses Skript herunterladen</a>  |  <a href="index.htm">Andere Beispiel-Skripte</a>  |  <a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>; Hier kann ein beliebiger Titel für das Menü angegeben werden:</em>
MenuTitle = -=-=-=-=-=-=-=-

<em>; Damit wird die Druckdauer der Maustaste bestimmt, bis das Menü angezeigt wird:</em>
UMDelay = 20

SetFormat, float, 0.0
SetBatchLines, 10ms
SetTitleMatchMode, 2
#SingleInstance


<em>;___________________________________________
;_____Menü-Definitionen_____________________</em>

<em>; Hier können die Menüpunkte erstellt oder bearbeitet werden.
; Es dürfen keine Leerzeichen im Key-/Wert-/Sektionsnamen verwendet werden.</em>

<em>; Machen Sie sich keine Sorgen über die Reihenfolge, das Menü wird sortiert.</em>

MenuItems = Editor/Rechner/Bereich 3/Bereich 4/Bereich 5


<em>;___________________________________________
;______Hier dynamische Menüpunkte___________</em>

<em>; Syntax:
;     Dyn# = Menüpunkt|Fenstertitel</em>

Dyn1 = MS Word|- Microsoft Word
Dyn2 = Editor II|- Editor

<em>;___________________________________________</em>

Exit


<em>;___________________________________________
;_____Menübereiche__________________________</em>

<em>; Hier können die Menübereiche erstellt oder bearbeitet werden.</em>

Editor:
Run, Notepad.exe
Return

Rechner:
Run, Calc
Return

Bereich3:
MsgBox, 3 ausgewählt
Return

Bereich4:
MsgBox, 4 ausgewählt
Return

Bereich5:
MsgBox, 5 ausgewählt
Return

MSWord:
MsgBox, Das ist ein dynamischer Eintrag (Word)
Return

EditorII:
MsgBox, Das ist ein dynamischer Eintrag (Editor)
Return


<em>;___________________________________________
;_____Hotkey-Bereich________________________</em>

~MButton::
HowLong = 0
Loop
{
    HowLong ++
    Sleep, 10
    GetKeyState, MButton, MButton, P
    IfEqual, MButton, U, Break
}
IfLess, HowLong, %UMDelay%, Return


<em>; Dynamisches Menü vorbereiten</em>
DynMenu =
Loop
{
    IfEqual, Dyn%A_Index%,, Break

    StringGetPos, ppos, dyn%A_Index%, |
    StringLeft, item, dyn%A_Index%, %ppos%
    ppos += 2
    StringMid, win, dyn%A_Index%, %ppos%, 1000

    IfWinActive, %win%,
        DynMenu = %DynMenu%/%item%
}


<em>; Sortiertes Hauptmenü mit dynamisches Menü verbinden</em>
Sort, MenuItems, D/
TempMenu = %MenuItems%%DynMenu%


<em>; Frühere Einträge entfernen</em>
Loop
{
    IfEqual, MenuItem%A_Index%,, Break
    MenuItem%A_Index% =
}

<em>; Neue Einträge erstellen</em>
Loop, Parse, TempMenu, /
{
    MenuItem%A_Index% = %A_LoopField%
}

<em>; Das Menü erstellen</em>
Menu = %MenuTitle%
Loop
{
    IfEqual, MenuItem%A_Index%,, Break
    numItems ++
    StringTrimLeft, MenuText, MenuItem%A_Index%, 0
    Menu = %Menu%`n%MenuText%
}

MouseGetPos, mX, mY
Hotkey, ~LButton, MenuClick
Hotkey, ~LButton, On
ToolTip, %Menu%, %mX%, %mY%
WinActivate, %MenuTitle%
Return


MenuClick:
Hotkey, ~LButton, Off
IfWinNotActive, %MenuTitle%
{
    ToolTip
    Return
}

MouseGetPos, mX, mY
ToolTip
mY -= 3        <em>; Platz, bevor die erste Zeile startet</em>
mY /= 13    <em>; Benötigter Platz jeder Zeile</em>
IfLess, mY, 1, Return
IfGreater, mY, %numItems%, Return
StringTrimLeft, TargetSection, MenuItem%mY%, 0
StringReplace, TargetSection, TargetSection, %A_Space%,, A
Gosub, %TargetSection%
Return
</pre>
</body>
</html>
